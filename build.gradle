plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

repositories {
    jcenter()
}

group = 'de.spinscale.javalin'
version = '0.2.0-SNAPSHOT'

dependencies {
    runtime 'io.jsonwebtoken:jjwt-impl:0.10.7'
    runtime 'io.jsonwebtoken:jjwt-orgjson:0.10.7'

    compile 'io.javalin:javalin:3.6.0'
    compile 'io.jsonwebtoken:jjwt-api:0.10.7'

    testCompile "org.assertj:assertj-core:3.14.0"
    testCompile 'org.mockito:mockito-core:3.2.4'
    testCompile 'org.slf4j:slf4j-simple:1.7.26'
    testCompile 'com.squareup.okhttp3:okhttp:4.2.2'

    // jupiter support
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.findProperty('nexusUsername')
                password project.findProperty('nexusPassword')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Cookie based session store for Javalin'
                description = 'Allows to store session data in a JWT based cookie'
                url = 'https://github.com/spinscale/javalin-cookie-session-store'

                scm {
                    connection = 'scm:git:git://github.com/spinscale/javalin-cookie-session-store.git'
                    developerConnection = 'scm:git:ssh://github.com/spinscale/javalin-cookie-session-store.git'
                    url = 'https://github.com/spinscale/javalin-cookie-session-store'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'spinscale'
                        name = 'Alexander Reelsen'
                        email = 'alr@spinscale.de'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
